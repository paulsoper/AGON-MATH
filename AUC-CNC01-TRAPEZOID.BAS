   10 REM -----------------------------------------------
   20 REM
   30 REM AUC-CNC01-TRAPEZOID.BAS
   40 REM
   50 REM USE THE TRAPEZOID METHOD - ALSO CALLED THE
   60 REM FIRST DEGREE CLOSED NEWTON-COTES METHOD - 
   70 REM TO FIND THE AREA UNDER A CURVE
   80 REM
   90 REM -- PAUL SOPER - AUGUST 23, 2024
  100 REM
  110 REM -----------------------------------------------
  120 :
  130 REM WE WILL USE AS AN EXAMPLE THE CURVE
  140 REM F(X) = 5X^3-12X^2+7X-3
  150 DEF FN_F(X) = 5.0*X*X*X-12.0*X*X+7.0*X-3.0
  160 :
  170 REM X1 AND X2 ARE THE END POINTS FOR THE DEFINITE INTEGRAL
  180 X1 = 2.0 : X2 = 6.0
  190 :
  200 REM N% WILL BE THE NUMBER OF SEGMENTS
  210 N% = 10
  220 :
  230 REM W IS THE WIDTH OF EACH SEGMENT
  240 W = (X2-X1)/N%
  250 :
  260 AREA = 0
  270 :
  280 FOR I% = 0 TO (N%-1)
  290   :
  300   A = X1 + I%*W
  305   B = A + W
  310   :
  320   AREA = AREA + (W/2) * (FN_F(A) + FN_F(B))
  325   :
  330 NEXT I%
  335 :
  340 PRINT "AREA CALCULATED BY TRAPEZOID RULE WITH "; N% " SEGMENTS: " AREA
  345 :
  350 REM G'(X) = F(X), SO G(X) = (5/4)X^4 - 4X^3 + (7/2)X^2 - 3X + C
  360 REM G(X2) - G(X1) = 864.0 - (-4.0) = 868.0
  370 :
  380 PRINT "ANALYTIC SOLUTION IS 868.0"
